group 'io.flutter.plugins.firebasemessaging'
version '1.0-SNAPSHOT'

buildscript {
  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.4'
  }
}

rootProject.allprojects {
  repositories {
    google()
    jcenter()
    maven { url 'https://developer.huawei.com/repo/' }
    flatDir {
      dirs project(':firebase_messaging').file('aars')
    }
  }
}

apply plugin: 'com.android.library'

def firebaseCoreProject = findProject(':firebase_core')
if (firebaseCoreProject == null) {
  throw new GradleException('Could not find the firebase_core FlutterFire plugin, have you added it as a dependency in your pubspec?')
} else if (!firebaseCoreProject.properties['FirebaseSDKVersion']) {
  throw new GradleException('A newer version of the firebase_core FlutterFire plugin is required, please update your firebase_core pubspec dependency.')
}

def getRootProjectExtOrCoreProperty(name, firebaseCoreProject) {
  if (!rootProject.ext.has('FlutterFire')) return firebaseCoreProject.properties[name]
  if (!rootProject.ext.get('FlutterFire')[name]) return firebaseCoreProject.properties[name]
  return rootProject.ext.get('FlutterFire').get(name)
}

android {
  compileSdkVersion 29
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  defaultConfig {
    minSdkVersion 19
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  lintOptions {
    disable 'InvalidPackage'
  }
  dependencies {
    api firebaseCoreProject
    implementation platform("com.google.firebase:firebase-bom:${getRootProjectExtOrCoreProperty("FirebaseSDKVersion", firebaseCoreProject)}")
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation fileTree(dir: 'libs', include: '*.jar')
    compileOnly(name: 'push_oppo-2.1.0', ext: 'aar')
    compileOnly(name: 'push_vivo-2.9.0.0', ext: 'aar')
    implementation 'com.huawei.hms:push:5.0.4.302'
  }
}

apply from: file("./user-agent.gradle")
